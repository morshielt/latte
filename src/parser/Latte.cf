-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;


-- ClassExtDef.   TopDef ::= "class" Ident "extends" Ident "{" [ClsStmt] "}" ;

-- ClassDef.      TopDef ::= "class" Ident "{" [ClsStmt] "}" ;

-- FnProp.        ClsStmt ::= FnDef ;

-- AttrProp.      ClsStmt ::= Type Ident ";" ;

-- separator nonempty ClsStmt "";

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LValue "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;  -- czy tu też LValue zamiast Ident

Decr.      Stmt ::= Ident "--"  ";" ;  -- czy tu też LValue zamiast Ident

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.	   Stmt ::= "for" "(" Arg ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Arr.     Type ::= Type "[]" ;

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ; -- powoduje unused rules

separator  Type "," ;

-- Expressions ---------------------------------------------

ArrAcc.    ArrAcc ::= Expr7 "[" Expr "]";

EArrNew.   Expr7 ::= "new" Type "[" Expr "]" ;

EArrGet.   Expr7 ::= ArrAcc;

EArrLen.   Expr6 ::= Expr7 "." "length";

EVar.      Expr7 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- lvalues -------------------------------------------------

LValIdent.        LValue ::= Ident ;
LValArr.          LValue ::= ArrAcc;

-- LValArr.          LValue ::= LValue "[" Expr "]";

-- ArrayElem.  ArrElemAccess ::= LValue "[" Expr "]" ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

